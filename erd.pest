comment = _{ "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
space = _{ " " }
tab = _ { "\t" }
ws = _{ space | tab }
empty = _{ ws* ~ NEWLINE }
char = {ASCII_DIGIT | ASCII_ALPHA_LOWER | ASCII_ALPHA_UPPER | "_"}
primary_key = { "*" }
foreign_key = { "+" }

// one, the other, or both (either order)
key_specifiers = { foreign_key? ~ primary_key | primary_key? ~ foreign_key}

word = @{ char+ }

tick = _{"'"}
btick = _{"`"}
quote = _{"\""}

bare_name = { word }
quoted_name = { word ~ ( ws+ ~ word)* }

identifier = @{
    btick ~ (!btick ~ quoted_name) ~ btick
  | tick ~ (!tick ~ quoted_name) ~ tick
  | quote ~ (!quote ~ quoted_name) ~ quote
  | bare_name
}

attribute = {key_specifiers? ~ identifier}

field = { !(entity_header | cardinality) ~ (ws* ~ attribute ~ ws*) ~ formatting? ~ ws* }
field_list = {
	((comment | empty)* ~ field)*
}

entity_header = { "[" ~ identifier ~ "]" ~ ws* ~ formatting? ~ ws* }

entity = {
  	(entity_header ~ ws* ~ NEWLINE)
  	~ field_list
}

one_exact = { "1" }
one_or_none = { "?" }
zero_or_more = { "*" }
one_or_more = { "+" }

card_type = {
      one_or_none
    | one_exact
    | zero_or_more
    | one_or_more
}

card_left = { identifier }
card_right = { identifier }
card_l_type = { card_type }
card_r_type = { card_type }

cardinality = {
    card_left ~
    	ws* ~
        card_l_type ~ "--" ~ card_r_type
        ~ ws*
    ~ card_right
}

formatting = { "{" ~ ws* ~(!"}" ~  format_rule_list? ) ~ NEWLINE? ~ ws* ~ "}" }

label = { "label" }
color = { "color" }
size = { "size" }
bgcolor = { "bgcolor" }
font = { "font" }
borderColor = { "border-color" }
border = { "border" }

format_key = {
    label
    | color
    | size
    | bgcolor
    | font
    | borderColor
    | border
}

format_value = {
 quote ~ (!quote ~ !NEWLINE ~ ANY)* ~ quote
}

format_rule = { format_key ~ ":" ~ ws* ~ format_value }

format_rule_list = {
    NEWLINE* ~ ws* ~
    format_rule ~ ws* ~
        ("," ~ ws* ~ NEWLINE* ~ ws* ~format_rule ~ ws* ~ NEWLINE*)*
}

erd = {
    SOI ~
    ((comment | empty)* ~ (entity | cardinality))*
    ~ EOI
}