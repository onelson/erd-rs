WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)*  ~ NEWLINE? }

char = {
    ASCII_DIGIT
    | ASCII_ALPHA_LOWER
    | ASCII_ALPHA_UPPER
    | "_"
}

primary_key = { "*" }
foreign_key = { "+" }

// one, the other, or both (either order)
key_specifiers = {
    foreign_key? ~ primary_key
    | primary_key? ~ foreign_key
}

word = @{ char+ }

tick = _{"'"}
btick = _{"`"}
quote = _{"\""}

bare_name = { word }
quoted_name = { word ~ (" " | word)* }

identifier = @{
    btick ~ (!btick ~ quoted_name) ~ btick
  | tick ~ (!tick ~ quoted_name) ~ tick
  | quote ~ (!quote ~ quoted_name) ~ quote
  | bare_name
}

attribute = {key_specifiers? ~ identifier}

field = { !(entity_header | cardinality) ~  attribute ~ formatting? }
field_list = {
	(field | NEWLINE)*
}

entity_header = { "[" ~ identifier ~ "]" ~ formatting? ~ NEWLINE? }
entity = { entity_header ~ field_list }

one_exact = { "1" }
one_or_none = { "?" }
zero_or_more = { "*" }
one_or_more = { "+" }

card_type = {
      one_or_none
    | one_exact
    | zero_or_more
    | one_or_more
}

card_left = { identifier }
card_right = { identifier }
card_l_type = { card_type }
card_r_type = { card_type }

cardinality = {
    card_left ~
        card_l_type ~ "--" ~ card_r_type
    ~ card_right
}

formatting = { "{" ~ (!"}" ~  format_rule_list? ) ~ "}" }

label = { "label" }
color = { "color" }
size = { "size" }
bgcolor = { "bgcolor" }
font = { "font" }
borderColor = { "border-color" }
border = { "border" }

format_key = {
    label
    | color
    | size
    | bgcolor
    | font
    | borderColor
    | border
}

format_value = @{
    quote ~ (!(quote | NEWLINE) ~ ANY)* ~ quote
}

format_rule = { format_key ~ ":" ~ format_value }

format_rule_list = {
    NEWLINE* ~
    format_rule ~
        ("," ~ NEWLINE* ~ format_rule )* ~ ","? ~ NEWLINE*
}

erd = {
    SOI ~
    (COMMENT | NEWLINE | entity | cardinality)*
    ~ EOI
}