WHITESPACE = _{ WHITE_SPACE }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }
space_no_new = _{ !NEWLINE ~ WHITE_SPACE }

char = {
    ASCII_DIGIT
    | ASCII_ALPHA_LOWER
    | ASCII_ALPHA_UPPER
    | "_"
}

ispk = { "*" }
isfk = { "+" }

// one, the other, or both (either order)
/// blue
keys = !{
    (isfk ~ ispk?)
    | (ispk ~ isfk?)
}

word = @{ char+ }

tick = _{ "'" }
btick = _{ "`" }
quote = _{ "\"" }

ident_no_space = { word }
ident_quoted = { word ~ (space_no_new | word)* }

ident = !{
    btick ~ (!btick ~ ident_quoted) ~ btick
  | tick ~ (!tick ~ ident_quoted) ~ tick
  | quote ~ (!quote ~ ident_quoted) ~ quote
  | ident_no_space
}

attr = @{ keys? ~ ident ~ space_no_new* ~ options? }

attribs = {
    // bail if we see something that looks like a relationship
    (!rel ~ attr)*
}



entity_name = @{ "[" ~ ident ~ "]" ~ space_no_new* ~ options? }

entity = {
    entity_name
    ~ attribs?
}

card_type = {
    "1"
    | "?"
    | "*"
    | "+"
}

/// blue
entity1 = !{ ident }
/// blue
entity2 = !{ ident }
/// yellow
card1 = !{ card_type }
/// red
card2 = !{ card_type }

rel = @{
    entity1
    ~ space_no_new*
        ~ card1 ~ "--" ~ card2
    ~ space_no_new*
    ~ entity2
    ~ space_no_new*
    ~ options?
}

opt_list = !{
    option ~ space_no_new*
    ~ (
        ("," ~ opt_list)
        | (","? ~ space_no_new* ~ COMMENT? ~ NEWLINE?)
    )
}

options = @{
    "{"
    ~ WHITESPACE*
    ~ opt_list?
    ~ WHITESPACE*
    ~ "}" ~ space_no_new* ~ COMMENT? ~ (EOI | NEWLINE)
}

/// red
opt_name = !{
    "label"
    | "color"
    | "bgcolor"
    | "size"
    | "font"
    | "border"
    | "border-color"
    | "cellspacing"
    | "cellborder"
    | "cellpadding"
    | "text-alignment"
}

/// green
opt_value = !{
    quoted_text
}

quoted_text = @{
    quote ~ (!quote ~ ANY)* ~ quote
}

option = @{ opt_name ~ ":" ~ space_no_new* ~  opt_value }


dir_type = !{
 "title"
 | "header"
 | "entity"
 | "relationship"
}

directive = @{
    dir_type ~ space_no_new* ~ options
}

// Header area of the document containing global formatting options (which will
// be merged in with "local" formatting options defined later).

head = {
    directive*
}

// Body contains the main ER data - entities and relationships.
body = {
    (rel | entity)*
}

document = {
    SOI
    ~ head
    ~ body
    ~ EOI
}