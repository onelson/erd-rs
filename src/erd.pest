WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)*  ~ NEWLINE? }

char = {
    ASCII_DIGIT
    | ASCII_ALPHA_LOWER
    | ASCII_ALPHA_UPPER
    | "_"
}

ispk = { "*" }
isfk = { "+" }

// one, the other, or both (either order)
/// green
keys = {
    (isfk ~ ispk?)
    | (ispk ~ isfk?)
}

word = @{ char+ }

tick = _{ "'" }
btick = _{ "`" }
quote = _{ "\"" }

ident_no_space = { word }
ident_quoted = { word ~ ( WHITESPACE | word)* }

ident = @{
    btick ~ (!btick ~ ident_quoted) ~ btick
  | tick ~ (!tick ~ ident_quoted) ~ tick
  | quote ~ (!quote ~ ident_quoted) ~ quote
  | ident_no_space
}

attr = { !(entity_name | rel) ~  keys? ~ ident ~ options? }
attribs = {
	(attr | NEWLINE)*
}

/// blue
entity = {
    entity_name ~ options?
    ~ NEWLINE
    ~ attribs?
}

/// yellow
entity_name = { "[" ~ ident ~ "]" }


one_exact = { "1" }
one_or_none = { "?" }
zero_or_more = { "*" }
one_or_more = { "+" }

card_type = {
      one_or_none
    | one_exact
    | zero_or_more
    | one_or_more
}

/// yellow
entity1 = { ident }
/// blue
entity2 = { ident }
card1 = { card_type }
card2 = { card_type }

/// red
rel = {
    !entity ~
    entity1 ~
        card1 ~ "--" ~ card2
    ~ entity2
    ~ options?
}

/// green
options = {
    "{"
    ~ !"}"
    ~ (NEWLINE* ~ opt ~ ("," ~ NEWLINE* ~ opt )* ~ ","? ~ NEWLINE*)?
    ~ "}"
}

label = { "label" }
color = { "color" }
size = { "size" }
bgcolor = { "bgcolor" }
font = { "font" }
border_color = { "border-color" }
border = { "border" }

opt_name = {
    label
    | color
    | size
    | bgcolor
    | font
    | border_color
    | border
}

opt_value = @{
    quote ~ (!(quote | NEWLINE) ~ ANY)* ~ quote
}

opt = { opt_name ~ ":" ~ opt_value }


dir_type_title = { "title" }
dir_type_header = { "header" }
dir_type_entity = { "entity" }
dir_type_relationship = { "relationship" }

/// red
dir_type = {
 dir_type_title
 | dir_type_header
 | dir_type_entity
 | dir_type_relationship
}

directive = {
    dir_type ~ options
}

// Header area of the document containing global formatting options (which will
// be merged in with "local" formatting options defined later).
head = {
    (COMMENT | NEWLINE | directive)*
}

// Body contains the main ER data - entities and relationships.
body = {
    (COMMENT | NEWLINE | entity | rel)*
}

document = {
    SOI
    ~ head
    ~ body
    ~ EOI
}